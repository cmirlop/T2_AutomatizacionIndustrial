FUNCTION_BLOCK "grafcet_40_final"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      Act_Proceso_Bueno : Bool;
      Act_Proceso_Malo : Bool;
      S4 : Bool;
      SigMaqLibre : Bool;
      Mantenimiento_Cinta : Bool;
      Mantenimiento_Trans : Bool;
      vaciado_cinta : Bool;
      vaciado_Trans : Bool;
      man_cinta : Bool;
      man_trans : Bool;
      CI : Bool;
      Reset : Bool;
      Resume : Bool;
   END_VAR

   VAR_OUTPUT 
      Maq_Libre : Bool;
      QC4 : Bool;
      QTRANS : Bool;
      Act_Sig_Maq : Bool;
      Alarma_Maquina : Bool;
      EstadoMaquina : Int;
   END_VAR

   VAR_IN_OUT 
      Alarma_1 : Bool;
   END_VAR

   VAR 
      S40 : Bool;
      S41 : Bool;
      S42 : Bool;
      S41b : Bool;
      S41c : Bool;
      S42b : Bool;
      X40 : Bool;
      X41 : Bool;
      X41b : Bool;
      X41c : Bool;
      X42 : Bool;
      X42b : Bool;
      S40e : Bool;
      S41e : Bool;
      S42e : Bool;
      X40e : Bool;
      X41e : Bool;
      X42e : Bool;
      S40a : Bool;
      S41a : Bool;
      S42a : Bool;
      S43a : Bool;
      X40a : Bool;
      X41a : Bool;
      X42a : Bool;
      X43a : Bool;
      FORCE_CURRENT : Bool;
      FORCE_CURRENT_E : Bool;
      FORCE_INIT : Bool;
      FORCE_INIT_E : Bool;
      STOP_ACT : Bool;
      RESET_ALARMA : Bool;
      Alarma : Bool;
      T1a {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
      T2a {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
      T3a {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
   END_VAR


BEGIN
	
	
	// EMERGENCIA
	#S40e := (#X42e AND #CI) OR "FirstScan";
	#S41e := #X40e AND "Emergencia";
	#S42e := #X41e;
	
	#X40e := #S40e OR (#X40e AND NOT #S41e);
	#X41e := #S41e OR (#X41e AND NOT #S42e);
	#X42e := #S42e OR (#X42e AND NOT #S40e);
	
	#FORCE_INIT_E := #X41e;
	#FORCE_CURRENT_E := #X42e;
	
	
	// ALARMAS
	#S40a := (#X43a AND NOT #FORCE_CURRENT_E) OR "FirstScan";
	#S41a := (#X40a AND #STOP_ACT) AND NOT #FORCE_CURRENT_E;
	#S42a := (#X41a AND #Reset) AND NOT #FORCE_CURRENT_E;
	#S43a := (#X42a OR (#X41a AND #Resume)) AND NOT #FORCE_CURRENT_E;
	
	#X40a := #S40a OR (#X40a AND NOT #S41a) OR #FORCE_INIT_E;
	#X41a := (#S41a OR (#X41a AND NOT #S42a AND NOT #S43a)) AND NOT #FORCE_INIT_E;
	#X42a := (#S42a OR (#X42a AND NOT #S43a)) AND NOT #FORCE_INIT_E;
	#X43a := (#S43a OR (#X43a AND NOT #S40a)) AND NOT #FORCE_INIT_E;
	
	#STOP_ACT := #X41a;
	#FORCE_INIT := #FORCE_INIT_E OR #X42a;
	#FORCE_CURRENT := #FORCE_CURRENT_E OR #X41a;
	#RESET_ALARMA := #X43a;
	
	
	
	// PRODUCCIÃ“N
	#S40 := ("FirstScan" OR (#X41c AND NOT #S4) OR (#X42b AND NOT #S4)) AND NOT #FORCE_CURRENT;
	#S41 := (#X40 AND #Act_Proceso_Bueno) AND NOT #FORCE_CURRENT;
	#S41b := (#X41 AND #S4) AND NOT #FORCE_CURRENT;
	#S41c := (#X41b AND #SigMaqLibre) AND NOT #FORCE_CURRENT;
	#S42 := (#X40 AND #Act_Proceso_Malo) AND NOT #FORCE_CURRENT;
	#S42b := (#X42 AND #S4) AND NOT #FORCE_CURRENT;
	
	
	
	#X40 := (#S40 OR #X40 AND (NOT #S41 AND NOT #S42)) OR #FORCE_INIT;
	#X41 := (#S41 OR #X41 AND (NOT #S41b)) AND NOT #FORCE_INIT;
	#X41b := (#S41b OR #X41b AND (NOT #S41c)) AND NOT #FORCE_INIT;
	#X41c := (#S41c OR #X41c AND (NOT #S40)) AND NOT #FORCE_INIT;
	#X42 := (#S42 OR #X42 AND (NOT #S42b)) AND NOT #FORCE_INIT;
	#X42b := (#S42b OR #X42b AND (NOT #S40)) AND NOT #FORCE_INIT;
	
	
	
	#Maq_Libre := #X40 AND NOT #STOP_ACT;
	#QC4 := (#X41 OR #X41c OR #X42 OR #Mantenimiento_Cinta OR #vaciado_cinta OR #man_cinta) AND NOT #STOP_ACT;
	#Act_Sig_Maq := #X41c AND NOT #STOP_ACT;
	#QTRANS := (#X42b OR #Mantenimiento_Trans OR #vaciado_Trans OR #man_trans) AND NOT #STOP_ACT;
	
	
	
	IF #X40 = TRUE AND NOT #Alarma AND NOT "Emergencia" THEN
	    #EstadoMaquina := 0;
	END_IF;
	IF #X40 = FALSE AND NOT #Alarma AND NOT "Emergencia" THEN
	    #EstadoMaquina := 1;
	END_IF;
	IF #Alarma AND NOT "Emergencia" THEN
	    #EstadoMaquina := 2;
	END_IF;
	IF "Emergencia" THEN
	    #EstadoMaquina := 3;
	END_IF;
	
	
	#T1a(IN:=(#X41 OR #X42) AND NOT #RESET_ALARMA,
	     PT:=T#4s);
	
	
	
	#Alarma_1 := (#Alarma_1 OR #T1a.Q) AND NOT #RESET_ALARMA;
	
	
	#Alarma := (#Alarma_1) AND NOT (#FORCE_INIT OR #RESET_ALARMA);
END_FUNCTION_BLOCK

